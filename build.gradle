/*
 * Copyright Â© 2017,2018 IBM Corp. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file
 * except in compliance with the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied. See the License for the specific language governing permissions
 * and limitations under the License.
 */

plugins {
   id "com.github.spotbugs" version "5.0.7"
   id 'java-library'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'
  apply plugin: 'signing'
  apply plugin: 'java-library'

  group = 'com.cloudant'
  version = new File(rootDir, 'VERSION').text.trim()

  // If the version says "snapshot" anywhere assume it is not a release
  ext.isReleaseVersion = !version.toUpperCase(Locale.ENGLISH).contains("SNAPSHOT")

  // 1.8 is ok for boot 1.x and 2.x
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenLocal()
    mavenCentral()
  }

  // Common dependencies
  ext.compileWithSpringVersion = '5.3.20'
  ext.compileWithBootVersion = '2.0.2.RELEASE'

  ext.testWithSpringVersion = '5.3.20'
  ext.testWithBootVersion = '1.5.13.RELEASE'

  test {
    useJUnitPlatform()
  }

  dependencies {
    }

  // Include variable debug info in the compiled classes
  compileJava.options.debugOptions.debugLevel = "source,lines,vars"
  // Fail on javac warnings
  compileJava.options.compilerArgs << "-Werror"

  // Always UTF-8
  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    sourceSets.all {
      into(name + "/java", { from allJava })
      into(name + "/resources", { from resources })
    }
  }

  javadoc {
    options.encoding 'UTF-8'
    options.setMemberLevel JavadocMemberLevel.PROTECTED
    // Add a logging listener to check for javadoc warnings and fail the build if there are any
    boolean hasJavaDocWarnings = false
    doFirst {
      getLogging().addStandardErrorListener(new StandardOutputListener() {
        void onOutput(CharSequence output) {
          if (output =~ "warning:") {
            hasJavaDocWarnings = true
          }
        }
      })
    }
    doLast {
      if (hasJavaDocWarnings) {
        throw new GradleException("Build failed due to javadoc warnings.")
      }
    }
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives sourcesJar, javadocJar
  }

  // Load signing and repository parameters from system properties
  ['signing.keyId', 'signing.password', 'signing.secretKeyRingFile', 'ossrhUsername', 'ossrhPassword']
        .each { propName ->
    //set a property with the given name if the system property is set
    //if the system property is not set then set the property to null if it isn't a signing one
    if (System.properties.(propName.toString()) != null || !propName.startsWith("signing")) {
      ext.(propName.toString()) = System.properties.(propName.toString())
    }
  }

// Load signing and repository parameters from system properties
  ['signing.keyId', 'signing.password', 'signing.secretKeyRingFile', 'ossrhUsername', 'ossrhPassword']
          .each { propName ->
            //set a property with the given name if the system property is set
            //if the system property is not set then set the property to null if it isn't a signing one
            if (System.properties.(propName.toString()) != null || !propName.startsWith("signing")) {
              ext.(propName.toString()) = System.properties.(propName.toString())
            }
          }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        pom {
          name = project.name
          description = 'Spring configuration for the official Cloudant library for Java'
          inceptionYear = '2017'
          packaging = 'jar'
          url = 'https://cloudant.com'
          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution = 'repo'
            }
          }
          scm {
            connection = 'scm:git:git://github.com/cloudant-labs/cloudant-spring.git'
            developerConnection = 'scm:git:git@github.com/cloudant-labs/cloudant-spring.git'
            url = 'https://github.com/cloudant-labs/cloudant-spring.git'
          }
          developers {
            developer {
              name = 'IBM Cloudant'
              email = 'cldtsdks@us.ibm.com'
              url = 'https://cloudant.com'
              organization = 'IBM'
              organizationUrl = 'http://www.ibm.com'
            }
          }
        }
      }
    }

    // Only sign release versions
    tasks.withType(Sign) {
      onlyIf { isReleaseVersion }
    }

    signing {
      // When signing, sign the archives
      sign publishing.publications.mavenJava
    }

    repositories {
      maven {
        url = version.endsWith('SNAPSHOT') ?
                "https://oss.sonatype.org/content/repositories/snapshots" :
                "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        credentials(PasswordCredentials) {
          username = ossrhUsername
          password = ossrhPassword
        }
      }
    }
  }

  // Findbugs
  apply plugin: 'com.github.spotbugs'
  spotbugs {
    toolVersion = "4.7.0"
    reportLevel = "low"
    effort = "max"
    includeFilter = file("findbugs-exclude.xml")
  }
/*
  tasks.withType(SpotBugsTask) {
    // Currently only one report type can be used toggle which with a property
    boolean generateXML = Boolean.getBoolean("spotbugs.xml.report")
    reports {
      xml.enabled = generateXML
      html.enabled = !generateXML
    }
  }
*/
  test { 
      systemProperty 'BOOT_VERSION', testWithBootVersion
      systemProperty 'FRAMEWORK_VERSION', testWithSpringVersion
  }
}


